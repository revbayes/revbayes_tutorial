################################################################################
#
# RevBayes Example: Bayesian inference of phylogeny using a general time reversible model
#                   with gamma distributed rate variation among sites
# 
# This file: Runs the full MCMC on a single gene under the general time reversible 
#            substitution model using an unconstrained non-clock tree.
#
# authors: Sebastian Hoehna, Michael Landis, Tracy A. Heath and Brian R. Moore
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readDiscreteCharacterData("data/primates_cytb.nex")

# Get some useful variables from the data. We need these later on.
n_species <- data.ntaxa()
taxa <- data.taxa()
n_branches <- 2 * n_species - 3

# set my move index
mi = 0



######################
# Substitution Model #
######################

#### specify the GTR+G substitution model applied uniformly to all sites ###
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)
moves[++mi] = mvSimplexElementScale(er,weight=3)

pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)
moves[++mi] = mvSimplexElementScale(pi,weight=2)

#### create a deterministic variable for the rate matrix ####
Q := fnGTR(er,pi) 

#### monitor difference between empirical and estimated base frequencies ####
pi_empirical <- data.getEmpiricalBaseFrequencies()
pi_diff := pi_empirical - pi



#############################
# Among Site Rate Variation #
#############################

alpha_prior <- 0.05
alpha ~ dnExponential( alpha_prior )
gamma_rates := fnDiscretizeGamma( alpha, alpha, 4, false )

# add moves the shape parameter
moves[++mi] = mvScale(alpha,weight=2)


###################
# Invariant sites #
###################

# what is the frequency of invariant patterns in the data?
finvar <- data.getNumInvariantSites() / data.nchar()

# create model parameter for proportion of invariant sites
pinvar ~ dnBeta(1,1)

# add move for pinvar
moves[++mi] = mvSlide(pinvar, weight=2.0)

# monitor the difference between empirical proportion of invariant patterns and the estimated proportion of invariant sites
invar_diff := finvar - pinvar


##############
# Tree model #
##############

# construct a variable for the tree drawn from a birth death process
topology ~ dnUniformTopology(taxa=taxa)

# add topology Metropolis-Hastings moves
moves[++mi] = mvNNI(topology, weight=1.0)
moves[++mi] = mvSPR(topology, weight=1.0)

# create branch length vector and add moves
for (i in 1:n_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves[++mi] = mvScale(br_lens[i])
}

# add deterministic node to monitor tree length
TL := sum(br_lens)

# unite topology and branch length vector into phylogeny object
phylogeny := treeAssembly(topology, br_lens)



###################
# PhyloCTMC Model #
###################

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=phylogeny, Q=Q, siteRates=gamma_rates, pInv=pinvar, type="DNA")

# attach the data
seq.clamp(data)



#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(Q)


monitors[1] = mnModel(filename="output/primates_cytb_GTR_Gamma_invar_non_clock.log",printgen=10, separator = TAB)
monitors[2] = mnFile(filename="output/primates_cytb_GTR_Gamma_invar_non_clock.trees",printgen=10, separator = TAB, phylogeny)
monitors[3] = mnScreen(printgen=1000, TL)

mymcmc = mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=10000,tuningInterval=1000)
mymcmc.run(generations=30000)


# Now, we will analyze the tree output.
# Let us start by reading in the tree trace
treetrace = readTreeTrace("output/primates_cytb_GTR_Gamma_invar_non_clock.trees", treetype="non-clock")
# and get the summary of the tree trace
#treetrace.summarize()

map_tree = mapTree(treetrace,"output/primates_cytb_GTR_Gamma_invar_non_clock.tree")

# you may want to quit RevBayes now
q()

