######################
# read in the data 
######################

time_tree <- readTrees("data/Primates_tree.nex")[1]
data <- readDiscreteCharacterData("data/Primates_morph.nex")

n_char <- data.nchar()


# in this example we are only going to use the first character of the matrix
data.excludeCharacter(5:n_char)
#data.includeCharacter(1)


# Get some useful variables from the data. We need these later on.
n_species = data.ntaxa()
taxa = data.taxa()
n_branches = 2 * n_species - 3


mvi = 0
mni = 0

n_sites = 4
n_states = 2


###################
# Molecular clock #
###################

### we use a uniform prior on the log-transform clock rate
log_clock_rate ~ dnUniform(-8,2)
moves[++mvi] = mvSlide(log_clock_rate, weight=2.0)
clock_rate := 10^log_clock_rate


######################
# Substitution Model #
######################

# Now create a deterministic variable for the nucleotide substitution rate matrix.
for (i in 1:n_sites) {
    pi[i] ~ dnDirichlet( rep(1,n_states) )
    moves[++mvi] = mvBetaSimplex(pi[i], weight=n_states)
    moves[++mvi] = mvDirichletSimplex(pi[i], offset=1, weight=n_states)
#    pi[i] <- simplex(1,1)
    Q[i] := fnF81( pi[i] )
}

for (i in 1:n_sites) {
    rf[i] ~ dnDirichlet( rep(1,n_states) )
    moves[++mvi] = mvBetaSimplex(rf[i], weight=n_states)
    moves[++mvi] = mvDirichletSimplex(rf[i], offset=1, weight=n_states)
}

# Create a stochastic node for the sequence evolution continuous-time Markov chain (CTMC).
for (i in 1:n_sites) {
    morph_ctmc[i] ~ dnPhyloCTMC(tree=time_tree, Q=Q[i], branchRates=clock_rate, rootFrequencies=rf[i], type="Standard")

    data_site[i] <- data
    data_site[i].excludeAll()
    data_site[i].includeCharacter(i)
    # and attach the sequence data
    morph_ctmc[i].clamp(data_site[i])
}


##################
# Finalize the integrated model and set up MCMC
##################

mymodel = model(time_tree)

# Create the monitors:
# First, a screen monitor with some useful variables:
monitors[++mni] = mnScreen(printgen=1000, clock_rate)

# And another monitor for all the model variables:
#monitors[++mni] = mnModel(filename="output/morph_MK_ind_rf.log", printgen=10)
monitors[++mni] = mnFile(filename="output/morph_MK_ind_rf.log", pi[1], pi[2], pi[3], pi[4], rf[1], rf[2], rf[3], rf[4], printgen=10)



# Now set up the MCMC.
mymcmc = mcmc(mymodel, monitors, moves)

### pre-burnin to tune the proposals
mymcmc.burnin(generations=5000,tuningInterval=500)

### run the MCMC
mymcmc.run(generations=20000)

mymcmc.operatorSummary()



q()
