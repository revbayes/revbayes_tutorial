#!/usr/bin/rb

# filenames
range_fn = "data/n6/silversword.n6.range.nex"
mol_fn   = "data/n6/silversword.mol.nex"
phy_fn   = "data/n6/silversword.tre"
out_fn   = "output/simple_phy"
geo_fn   = "data/n6/hawaii.n6"
times_fn = geo_fn + ".times.txt"
dist_fn  = geo_fn + ".distances.txt"

# move/monitor index
mvi = 1
mni = 1
n_gen = 1e5


########
# data #
########


# read molecular data
dat_mol = readDiscreteCharacterData(mol_fn)

# read binary (01) presence-absence range data
dat_range_01 = readDiscreteCharacterData(range_fn)
n_areas <- dat_range_01.nchar()

# determine the number of states
max_areas <- 2
n_states <- 0
for (k in 0:max_areas) n_states += choose(n_areas, k)

# convert binary ranges into NaturalNumbers
dat_range_n = formatDiscreteCharacterData(dat_range_01, "DEC", n_states)

# epoch times
time_bounds <- readDataDelimitedFile(file=times_fn, delimiter=" ")
n_epochs <- time_bounds.size()

# area distances
distances <- readDataDelimitedFile(file=dist_fn, delimiter=" ")

# tree
phy_init = readTrees(phy_fn)[1]

# taxa
taxa = dat_mol.taxa()
n_taxa = taxa.size()
n_branches = 2 * n_taxa - 2


###############
# Tree models #
###############

# root age
root_age ~ dnUniform(0, 15)
moves[mvi++] = mvScale(root_age, weight=2)

# proportion of sampled tips
rho <- 35 / 50

# birth rate
birth ~ dnExp(1)
moves[mvi++] = mvScale(birth)

# death rate
death ~ dnExp(1)
moves[mvi++] = mvScale(death)

# birth-death process
phy ~ dnBDP(lambda=birth, mu=death, rho=rho, rootAge=root_age, taxa=taxa)

# provide a starting tree (not essential)
phy.setValue(phy_init)
root_age.setValue(phy_init.rootAge())

# tree moves
moves[mvi++] = mvNNI(phy, weight=n_branches/2)
moves[mvi++] = mvFNPR(phy, weight=n_branches/8)
moves[mvi++] = mvNodeTimeSlideUniform(phy, weight=n_branches/2)


# monitor the age of the silversword ingroup
ingroup_clade <- clade("Wilkesia_hobdyi",
                       "Dubautia_reticulata",
                       "Dubautia_microcephala",
                       "Argyroxiphium_caliginis")

ingroup_age := tmrca(phy, ingroup_clade)



###################
# Molecular model #
###################

# molecular clock
log10_rate_mol ~ dnUniform(-6,3)
log10_rate_mol.setValue(-1)
moves[mvi++] = mvSlide(log10_rate_mol, weight=5, delta=0.2)
rate_mol := 10^log10_rate_mol

# relaxed clock
branch_sd <- 1.0
branch_mean <- 0 - 0.5*branch_sd^2
for (i in 1:n_branches) {
    branch_rate_multiplier[i] ~ dnLognormal(mean=branch_mean, sd=branch_sd)
    moves[mvi++] = mvScale(branch_rate_multiplier[i])
    branch_rates[i] := rate_mol * branch_rate_multiplier[i]
}

# HKY substitution process
kappa ~ dnGamma(2,2)
moves[mvi++] = mvScale(kappa)

bf ~ dnDirichlet( [1,1,1,1] )
moves[mvi++] = mvSimplex(bf, alpha=10, weight=2, offset=1)

Q_mol := fnHKY(kappa, bf)

# +Gamma-4 among site rate variation
alpha ~ dnUniform(0,50)
moves[mvi++] = mvScale(alpha)
site_rates := fnDiscretizeGamma(alpha, alpha, numCats=4)

# substitution process over phylogeny
seq_mol ~ dnPhyloCTMC(Q=Q_mol,
                      tree=phy,
                      branchRates=branch_rates,
                      siteRates=site_rates,
                      type="DNA",
                      nSites=dat_mol.nchar())

# observe multiple sequence alignemnt
seq_mol.clamp(dat_mol)

#######################
# Biogeography models #
#######################

rate_bg <- 1
log10_rate_bg ~ dnUniform(-4,2)
log10_rate_bg.setValue(-2)
rate_bg := 10^log10_rate_bg
moves[mvi++] = mvSlide(log10_rate_bg, weight=4)

if (false) {
log10_dispersal_rate ~ dnUniform(-4,2)
log10_dispersal_rate.setValue(-2)
dispersal_rate := 10^log10_dispersal_rate
moves[mvi++] = mvSlide(log10_dispersal_rate, weight=4)

log10_extirpation_rate ~ dnUnif(-6,3)
moves[mvi++] = mvSlide(log10_extirpation_rate, weight=4)
log10_extirpation_rate.setValue(-2)
extirpation_rate := 10^log10_extirpation_rate
}

dispersal_rate <- abs(1)

log_sd <- 0.5
log_mean <- ln(1) - 0.5*log_sd^2
extirpation_rate ~ dnLognormal(mean=log_mean, sd=log_sd)
moves[mvi++] = mvScale(extirpation_rate, weight=2)


# first, the distance function
for (i in 1:n_areas) {
    for (j in 1:n_areas) {
        er[i][j] <- abs(0) 
        dr[i][j] := dispersal_rate
    }
    er[i][i] := extirpation_rate
}

# build DEC rate matrices
Q_DEC := fnDECRateMatrix(dispersalRates=dr,
                         extirpationRates=er,
                         maxRangeSize=max_areas)

    
# build clado probs
clado_event_types <- [ "s", "a" ]
clado_event_probs <- simplex(1,1)
P_DEC := fnDECCladoProbs(eventProbs=clado_event_probs,
                         eventTypes=clado_event_types,
                         numCharacters=n_areas,
                         maxRangeSize=max_areas)


# root frequencies
rf_DEC    <- rep(1, n_states)
rf_DEC    <- simplex(rf_DEC)

m_bg ~ dnPhyloCTMCClado(tree=phy,
                           Q=Q_DEC,
                           cladoProbs=P_DEC,
                           branchRates=rate_bg,
                           rootFrequencies=rf_DEC,
                           type="NaturalNumbers",
                           nSites=1)
    
m_bg.clamp(dat_range_n)

# build the times
for (i in 1:n_epochs) {
    time_max[i] <- time_bounds[i][1]
    time_min[i] <- time_bounds[i][2]

    if (i == n_epochs) {
        epoch_times[i] <- abs(0)
    } else {
        epoch_times[i] ~ dnUniform(time_min[i], time_max[i])
        moves[mvi++] = mvSlide(epoch_times[i], delta=(time_bounds[i][1]-time_bounds[i][2])/2)
    }
}



############
# Monitors #
############

# monitor the age of the silversword ingroup
ingroup_clade <- clade("Wilkesia_hobdyi",
                       "Dubautia_reticulata",
                       "Dubautia_microcephala",
                       "Argyroxiphium_caliginis")

ingroup_age := tmrca(phy, ingroup_clade)
for (i in 1:n_epochs) {
    ingroup_older_island[i] := ifelse( ingroup_age > epoch_times[i], 1, 0 )
}


monitors[mni++] = mnScreen(printgen=100, ingroup_age)
monitors[mni++] = mnModel(file=out_fn+".model.log", printgen=100)
monitors[mni++] = mnFile(phy, filename=out_fn+".tre", printgen=100)
monitors[mni++] = mnJointConditionalAncestralState(tree=phy,
                                                       ctmc=m_bg,
                                                       type="NaturalNumbers",
                                                       withTips=true,
                                                       withStartStates=true,
                                                       filename=out_fn+".states.log",
                                                       printgen=100)

############
# Analysis #
############

mdl = model(m_bg, ingroup_older_island, epoch_times)
ch = mcmc(mdl, monitors, moves)

ch.run(n_gen)


quit()
