#!/usr/bin/rb

# filenames
range_fn = "data/n6/silversword.n6.range.nex"
mol_fn   = "data/n6/silversword.mol.nex"
tree_fn   = "data/n6/silversword.tre"
out_fn   = "output/epoch_phy_prior"
geo_fn   = "data/n6/hawaii.n6"
times_fn = geo_fn + ".times.txt"
dist_fn  = geo_fn + ".distances.txt"

# move/monitor index
mvi = 1
mni = 1
n_gen = 1e5


########
# data #
########


# read molecular data
dat_mol = readDiscreteCharacterData(mol_fn)

# read binary (01) presence-absence range data
dat_range_01 = readDiscreteCharacterData(range_fn)
n_areas <- dat_range_01.nchar()

# determine the number of states
max_areas <- 2
n_states <- 0
for (k in 0:max_areas) n_states += choose(n_areas, k)

# convert binary ranges into NaturalNumbers
dat_range_n = formatDiscreteCharacterData(dat_range_01, "DEC", n_states)

# epoch times
time_bounds <- readDataDelimitedFile(file=times_fn, delimiter=" ")
n_epochs <- time_bounds.size()

# epoch connectivity
for (i in 1:n_epochs) {
    epoch_fn = geo_fn + ".connectivity." + i + ".txt"
    connectivity[i] <- readDataDelimitedFile(file=epoch_fn, delimiter=" ")
}

# area distances
distances <- readDataDelimitedFile(file=dist_fn, delimiter=" ")

# tree
tree_init = readTrees(tree_fn)[1]

# taxa
taxa = tree_init.taxa()
n_taxa = taxa.size()
n_branches = 2 * n_taxa - 2


###############
# Tree models #
###############

# root age
root_age ~ dnUniform(0, 15)
moves[mvi++] = mvScale(root_age, weight=2)

# proportion of sampled tips
rho <- 35 / 50

# birth rate
birth ~ dnExp(1)
moves[mvi++] = mvScale(birth)

# death rate
death ~ dnExp(1)
moves[mvi++] = mvScale(death)

# birth-death process
tree ~ dnBDP(lambda=birth, mu=death, rho=rho, rootAge=root_age, taxa=taxa)

# provide a starting tree (not essential)
tree.setValue(tree_init)
root_age.setValue(tree_init.rootAge())

# tree moves
moves[mvi++] = mvNNI(tree, weight=n_branches/2)
moves[mvi++] = mvFNPR(tree, weight=n_branches/8)
moves[mvi++] = mvNodeTimeSlideUniform(tree, weight=n_branches/2)




###################
# Molecular model #
###################

# molecular clock
log10_rate_mol ~ dnUniform(-6,3)
log10_rate_mol.setValue(-1)
moves[mvi++] = mvSlide(log10_rate_mol, weight=5, delta=0.2)
rate_mol := 10^log10_rate_mol

# relaxed clock
branch_sd <- 1.0
branch_mean <- 0 - 0.5*branch_sd^2
for (i in 1:n_branches) {
    branch_rate_multiplier[i] ~ dnLognormal(mean=branch_mean, sd=branch_sd)
    moves[mvi++] = mvScale(branch_rate_multiplier[i])
    branch_rates[i] := rate_mol * branch_rate_multiplier[i]
}

# HKY substitution process
kappa ~ dnGamma(2,2)
moves[mvi++] = mvScale(kappa)

bf ~ dnDirichlet( [1,1,1,1] )
moves[mvi++] = mvSimplex(bf, alpha=10, weight=2, offset=1)

Q_mol := fnHKY(kappa, bf)

# +Gamma-4 among site rate variation
alpha ~ dnUniform(0,50)
moves[mvi++] = mvScale(alpha)
site_rates := fnDiscretizeGamma(alpha, alpha, numCats=4)

# substitution process over phylogeny
seq_mol ~ dnPhyloCTMC(Q=Q_mol,
                      tree=tree,
                      branchRates=branch_rates,
                      siteRates=site_rates,
                      type="DNA",
                      nSites=dat_mol.nchar())

# observe multiple sequence alignemnt
seq_mol.clamp(dat_mol)

#######################
# Biogeography models #
#######################


# anagenetic event rate scaler
log10_rate_bg ~ dnUniform(-4,2)
log10_rate_bg.setValue(-2)
rate_bg := 10^log10_rate_bg
moves[mvi++] = mvSlide(log10_rate_bg, weight=4)

# fix relative dispersal rate to 1
dispersal_rate <- 1.0

# geographic distance scaler
distance_scale ~ dnUnif(0,20)
distance_scale.setValue(0.001)
moves[mvi++] = mvScale(distance_scale, weight=3)

# then, the dispersal rate matrix
for (i in 1:n_epochs) {
    for (j in 1:n_areas) {
        for (k in 1:n_areas) {
            dr[i][j][k] <- 0.0
            if (connectivity[i][j][k] > 0) {
                dr[i][j][k] := dispersal_rate * exp(-distance_scale * distances[j][k])
            }
        }
    }
}

# then relative extirpation rates (or per-area extinction rates)
log_sd <- 0.5
log_mean <- ln(1) - 0.5*log_sd^2
extirpation_rate ~ dnLognormal(mean=log_mean, sd=log_sd)
moves[mvi++] = mvScale(extirpation_rate, weight=2)

# the extirpation rate matrix
for (i in 1:n_epochs) {
    for (j in 1:n_areas) {
        for (k in 1:n_areas) {
            er[i][j][k] <- 0.0
        }
        er[i][j][j] := extirpation_rate
    }
}

# build DEC rate matrices
for (i in 1:n_epochs) {
    Q_DEC[i] := fnDECRateMatrix(dispersalRates=dr[i],
                                extirpationRates=er[i],
                                maxRangeSize=max_areas)
}

# build the epoch times
for (i in 1:n_epochs) {
    time_max[i] <- time_bounds[i][1]
    time_min[i] <- time_bounds[i][2]

    if (i == n_epochs) {
        epoch_times[i] <- 0.0
    } else {
        epoch_times[i] ~ dnUniform(time_min[i], time_max[i])
        moves[mvi++] = mvSlide(epoch_times[i], delta=(time_bounds[i][1]-time_bounds[i][2])/2)
    }
}

# combine the epoch rate matrices and times
Q_DEC_epoch := fnEpoch(Q=Q_DEC, times=epoch_times, rates=rep(1, n_epochs))

# build clado probs
clado_event_types <- [ "s", "a" ]
p_sympatry ~ dnUniform(0,1)
p_allopatry := abs(1.0 - p_sympatry)
moves[mvi++] = mvSlide(p_sympatry, delta=0.1, weight=2)
clado_event_probs := simplex(p_sympatry, p_allopatry)
P_DEC := fnDECCladoProbs(eventProbs=clado_event_probs,
                         eventTypes=clado_event_types,
                         numCharacters=n_areas,
                         maxRangeSize=max_areas)

# root frequencies
rf_DEC    <- rep(0, n_states)
rf_DEC[n_areas+1] <- 1  # Mainland (Z) is the only possible starting state
rf_DEC    <- simplex(rf_DEC)

# the phylogenetic CTMC with cladogenetic events
m_bg ~ dnPhyloCTMCClado(tree=tree,
                           Q=Q_DEC_epoch,
                           cladoProbs=P_DEC,
                           branchRates=rate_bg,
                           rootFrequencies=rf_DEC,
                           type="NaturalNumbers",
                           nSites=1)

# attach the range data
m_bg.clamp(dat_range_n)



############
# Monitors #
############

# monitor the age of the silversword ingroup
ingroup_clade <- clade("Wilkesia_hobdyi",
                       "Dubautia_reticulata",
                       "Dubautia_microcephala",
                       "Argyroxiphium_caliginis")

ingroup_age := tmrca(tree, ingroup_clade)
for (i in 1:n_epochs) {
    ingroup_older_island[i] := ifelse(ingroup_age > epoch_times[i], 1, 0)
}

monitors[mni++] = mnScreen(printgen=100, dispersal_rate, distance_scale, ingroup_age)
monitors[mni++] = mnModel(file=out_fn+".model.log", printgen=100)
monitors[mni++] = mnFile(tree, filename=out_fn+".tre", printgen=100)
monitors[mni++] = mnJointConditionalAncestralState(tree=tree,
                                                       ctmc=m_bg,
                                                       type="NaturalNumbers",
                                                       withTips=true,
                                                       withStartStates=true,
                                                       filename=out_fn+".states.log",
                                                       printgen=100)

############
# Analysis #
############

# build the model analysis object from the model graph
mymodel = model(m_bg, ingroup_older_island)

# create the MCMC analysis object
mymcmc = mcmc(mymodel, monitors, moves)

# run the MCMC analysis
mymcmc.run(n_gen)

#################
# Post-Analysis #
#################

# get the converted state descriptions
state_desc = dat_range_n.getStateDescriptions()

# write the state descriptions to file
state_desc_str = "state,range\n"
for (i in 1:state_desc.size())
{
    state_desc_str += (i-1) + "," + state_desc[i] + "\n"
}
write(state_desc_str, file=out_fn+".state_labels.txt")

# exit
quit()
