################################################################################
#
# RevBayes Example: Bayesian inference of diversification rates under a
#                   constant-rate pure-birth model
# 
#
# authors: Sebastian Hoehna and Tracy A. Heath
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

T <- readTrees("data/primates_Springer.tre")[1]

# Get some useful variables from the data. We need these later on.
taxa <- T.taxa()

# set my move index
mvi = 0


######################
# pure-birth  model  #
######################

### the birth rate is a stochastic random variable drawn from a lognormal prior
### MCMC samples this variable using a scale proposal
birth_rate_mean <- ln( ln(450.0/2.0) / T.rootAge() )
birth_rate_sd <- 0.587405
birth_rate ~ dnLognormal(mean=birth_rate_mean,sd=birth_rate_sd) 
moves[++mvi] = mvScale(birth_rate,lambda=1.0,tune=true,weight=3.0)

### rho is the probability of sampling species at the present
### fix this to 367/450, since there are ~450 described species of primates
### and we have sampled 367
rho <- T.ntips()/450

### the BDP is conditioned on the root time, we can get this value from our tree and set a constant node
root_time <- T.rootAge()

### the time tree is a stochastic node modeled by the constant rate birth-death process (dnBDP)
### by setting mu to the constant value 0.0, we are specifying a pure-birth process
timetree ~ dnBDP(lambda=birth_rate, mu=0.0, rho=rho, rootAge=root_time, samplingStrategy="uniform", condition="survival", taxa=taxa)

### clamp the model with the "observed" tree
timetree.clamp(T)



#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(birth_rate)

### set up the monitors that will output parameter values to file and screen 
monitors[1] = mnModel(filename="output/primates_Yule.log",printgen=10, separator = TAB)
monitors[2] = mnScreen(printgen=1000, birth_rate)



################
# The Analysis #
################

### compute power posterior distributions
pow_p = powerPosterior(mymodel, moves, monitors, "output/Yule_powp.out", cats=100, sampleFreq=10) 
pow_p.burnin(generations=10000,tuningInterval=200)
pow_p.run(generations=10000)  

### Use stepping-stone sampling to calculate marginal likelihoods
ss = steppingStoneSampler(file="output/Yule_powp.out", powerColumnName="power", likelihoodColumnName="likelihood")
ss.marginal() 

### Use path-sampling to calculate marginal likelihoods
ps = pathSampler(file="output/Yule_powp.out", powerColumnName="power", likelihoodColumnName="likelihood")
ps.marginal() 




## quit ##
q()